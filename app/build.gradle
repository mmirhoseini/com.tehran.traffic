apply plugin: 'com.android.application'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.tehran.traffic"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion

        versionCode 32
        versionName '5.0.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '_debug'
        }
    }

    flavorDimensions "ads"

    productFlavors {
        play{
            dimension "ads"
        }

        bazaar{
            dimension "ads"
        }
    }

    signingConfigs {
        buildTypes {
            debug {
                buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
                buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
            }
            release {
                buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
                buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
            }
        }
    }

    /* fixes espresso conflict with supportAnnotation and androidSupport new versions */
    configurations.all {
        resolutionStrategy {
            force "com.android.support:support-annotations:${androidSupportVersion}"
            force "com.android.support:appcompat-v7:${androidSupportVersion}"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation "com.android.support:support-annotations:${androidSupportVersion}"
    implementation 'org.jbundle.util.osgi.wrapped:org.jbundle.util.osgi.wrapped.org.apache.http.client:4.1.2'
    implementation "com.mirhoseini.appsettings:appsettings:${appSettingsVersion}"
    implementation "com.mirhoseini.navigationview:navigationview:${navigationViewVersion}"
    implementation "com.mirhoseini.utils:utils:${utilsVersion}"
    implementation "com.google.firebase:firebase-messaging:${playServicesVersion}"
    implementation "com.google.firebase:firebase-ads:${playServicesVersion}"
}
apply plugin: 'com.google.gms.google-services'
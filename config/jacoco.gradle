apply plugin: 'jacoco'
apply from: "$rootDir/config/dependencies.gradle"

jacoco {
    toolVersion = "$jacoco_version"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

tasks.create(name: "jacoco", type: JacocoReport, dependsOn: ["testDebugUnitTest", "createDebugCoverageReport"]) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for build."

    reports {
        html.enabled = true
        xml.enabled = true
    }

    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            'androidx/**/*.*',
            '**/BR.*',
            '**/*Binding.*',
            '**/*Binding*Impl.*',
            '*_.*',
            '**/*Directions.*',
            '**/*Args.*'
    ]
    def javaClasses = fileTree(dir: "$project.buildDir/intermediates/javac/debug", excludes: excludes)
    def kotlinClasses = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: excludes)
    getClassDirectories().setFrom(files([javaClasses, kotlinClasses]))

    getSourceDirectories().setFrom(files([
            "$project.projectDir/src/main/java",
    ]))

    def includes = [
            'jacoco/testDebugUnitTest.exec',
            'outputs/code_coverage/debugAndroidTest/connected/**/*.ec'
    ]
    getExecutionData().setFrom(fileTree(dir: project.buildDir, includes: includes))
}
